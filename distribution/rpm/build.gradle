task buildRpm(type: Rpm) {
  dependsOn dependencyFiles, preparePackagingFiles
  baseName 'kafka' // this is what pom generation uses for artifactId
  // Follow kafka's rpm file naming convention
  archiveName "${packageName}-${project.version}.rpm"
  packageGroup 'Application/Internet'
  prefix '/usr'
  packager 'Kafka'
  if (version.contains('~')) {
    def tokenized = version.tokenize('~')
    version tokenized[0]
    release tokenized[1]
  } else {
    version version
    release '1'
  }
  arch 'noarch'
  os 'linux'
  distribution 'Kafka'
  dirMode 0755
  fileMode 0644
  addParentDirs false
  // TODO ospackage doesn't support icon but we used to have one
}

artifacts {
  'default' buildRpm
  archives buildRpm
}

integTest {
  /* We use real rpm tools to extract the rpm file for testing so we have to
    skip the test if they aren't around. */
  enabled = new File('/bin/rpm').exists() ||   // Standard location
      new File('/usr/bin/rpm').exists() ||     // Debian location
      new File('/usr/local/bin/rpm').exists()  // Homebrew location
}
